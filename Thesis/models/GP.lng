!Name: GP entity allocation model;
!Note: PLI constraints linearized and FMCU transformed as a function of ROS;

MODEL:

!Define the sets;

SETS:
DEVIATION_SET_SHORT / 1..2/: P;
DEVIATION_SET / 1..3/: NM,PM;

DECISION_SET / 1..142/:XC,XR;
DATA_SET/1..142/:TX,CE,LQ,UQ,MQ,PLI;

ENDSETS

DATA:

rev=@OLE('ExLingo.xlsx', 'rev');
emp=@OLE('ExLingo.xlsx', 'emp');

PLI=@OLE('ExLingo.xlsx', 'pli');
TX=@OLE('ExLingo.xlsx', 'ct');
CE=@OLE('ExLingo.xlsx', 'gni');
LQ=@OLE('ExLingo.xlsx', 'pl');
UQ=@OLE('ExLingo.xlsx', 'pu');
MQ=@OLE('ExLingo.xlsx', 'pm');

w1=0.16;
w2=0.30;
w3=0.54;

ENDDATA

MIN = (P(1)*w1)+(P(2)*w2) 
+ (@SUM(DEVIATION_SET(I): (NM(I)+PM(I)))*w3);

SUM = (P(1))+(P(2)) 
+ (@SUM(DEVIATION_SET(I): (NM(I)+PM(I))));


!Cost minimization;
@SUM( DECISION_SET (I) :
 (XC(I)*CE(I))
) - P(1) =0;

!Tx base min;
@SUM( DECISION_SET (I) :
 ((XR(I) - (XC(I)*CE(I))) * TX(I))
) - P(2) =0;

!PLI allocation Linearized;
@FOR(DECISION_SET(I):  
((XR(I) - (XC(I)*CE(I))))
>= LQ(I) * XR(I));


@FOR(DECISION_SET(I):  
((XR(I) - (XC(I)*CE(I))))
<= UQ(I) * XR(I));

!Value chain;

0.13*(rev-@SUM(DECISION_SET(I) : XC(I)*CE(I)))=@SUM(DECISION_SET(I) : 
@IF(PLI(I) #EQ# 1, XR(I)-XC(I), 0
))
+NM(1)-PM(1);

0.19*(rev-@SUM(DECISION_SET(I) : XC(I)*CE(I)))=@SUM(DECISION_SET(I) : 
@IF(PLI(I) #EQ# 2, XR(I)-XC(I), 0
))
+NM(2)-PM(2);

0.68*(rev-@SUM(DECISION_SET(I) : XC(I)*CE(I)))=@SUM(DECISION_SET(I) : 
@IF(PLI(I) #EQ# 3, XR(I)-XC(I), 0
))
+NM(3)-PM(3);

@FOR(DEVIATION_SET_SHORT(I) : P(I)>=0);
@FOR(DEVIATION_SET(I) : NM(I)>=0);
@FOR(DEVIATION_SET(I) : PM(I)>=0);

rev = @SUM(DECISION_SET(I) : XR(I));
emp = @SUM(DECISION_SET(I) : XC(I));

